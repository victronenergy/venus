# Parallelism Options (default to number of cores)
BB_NUMBER_THREADS ?= "${@oe.utils.cpu_count()}"
PARALLEL_MAKE ?= "-j ${@oe.utils.cpu_count()}"

# Optional include a host wide config.
#
# Be careful with this, it can drive you nuts why something will work in one
# place, but not in another. It is usefull though for specific things, e.g.
# linking nodejs needs several tens of / hundred Gigabytes to link if you get
# unlucky. PARALLEL_MAKE:pn-nodejs = "-j 2" can prevent that...
include ${HOME}/.config/venus/local.conf

# This sets the default machine if no other machine is selected
# Allow the include files to set the MACHINE
MACHINE ??= "ccgx"

# Where to place downloads (the autobuilder overwrites this, so keep ?= )
DL_DIR ?= "${TOPDIR}/../../oe-downloads/venus"

DEPLOY_DIR = "${TOPDIR}/../deploy/${DISTRO}"
PERSISTENT_DIR_PREFIX ?= "${TOPDIR}/../persistent"
PERSISTENT_DIR = "${PERSISTENT_DIR_PREFIX}/${DISTRO}"

DISTRO ?= "venus"

PACKAGE_DEBUG_SPLIT_STYLE = "debug-file-directory"

# SDK/ADT target architecture (and prevent building X)
SDKMACHINE ?= "x86_64"
ASSUME_PROVIDED += "virtual/libx11 consolekit"

include extra-options.conf
include conf/${DISTRO}.conf

# By default OE will use a fixed timestamp, causing the last-modification
# time of the webservers to never change even if the file did change.
# For now, set the time to modification time of venus.conf, so it will
# change on every version change (but that is actually too often).
REPRODUCIBLE_TIMESTAMP_ROOTFS := "${@int(os.stat(d.expand('${TOPDIR}/../sources/meta-victronenergy/meta-venus/conf/distro/venus.conf')).st_mtime)}"

#include conf/local/use-host-tools.conf
